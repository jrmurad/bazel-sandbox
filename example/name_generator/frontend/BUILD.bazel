load("@build_bazel_rules_nodejs//:index.bzl", "copy_to_bin", "npm_package_bin")
load("@npm//@bazel/typescript:index.bzl", "ts_project")
load("//:tools/jest.bzl", "jest_test")

ts_project(
    name = "frontend",
    srcs = glob(
        ["**/*.tsx"],
        exclude = ["**/*.stories.tsx"],
    ),
    declaration = True,
    tsconfig = "//:tsconfig.json",
    visibility = ["//example:__subpackages__"],
    deps = [
        "//common/frontend:rpc_context",
        "//example/name_generator/proto/v1:example_name_generator_proto_v1_ts_proto",
        "@npm//@types/react",
        "@npm//react",
    ],
)

copy_to_bin(
    name = "stories",
    srcs = glob(["**/*.stories.tsx"]),
)

npm_package_bin(
    name = "storybook",
    args = [
        "--config-dir $(BINDIR)/.storybook",
        "--output-dir $(@D)",
    ],
    data = [
        ":frontend",
        ":stories",
        "//:storybook_data_copy",
        "//:tsconfig.json",
        "@npm//tsconfig-paths-webpack-plugin",
    ],
    output_dir = True,
    tool = "@npm//@storybook/react/bin:build-storybook",
)

jest_test(
    name = "storyshots",
    srcs = [
        "//:storyshots.puppeteer.test.ts",
        "//:storyshots.test.ts",
    ],
    args = ["--storybook=$(rootpath :storybook)"],
    data = [
        ":storybook",
        ":frontend",
        "//:.storybook",
        "//:babel.config.js",
        "//:package.json",
        "@npm//@babel/plugin-proposal-class-properties",
        "@npm//@babel/preset-env",
        "@npm//@babel/preset-react",
        "@npm//@babel/preset-typescript",
        "@npm//@storybook/addon-storyshots",
        "@npm//@storybook/addon-storyshots-puppeteer",
        "@npm//@storybook/react",
        "@npm//testcontainers",
        "@npm//tsconfig-paths-webpack-plugin",
        "@npm//yargs",
    ] + glob([
        "**/*.stories.tsx",
        "**/__image_snapshots__/*.png",
        "**/__snapshots__/*.storyshot",
    ]),
)
